/* analysisMP25.do               damiancclarke             yyyy-mm-dd:2023-02-20
----|----1----|----2----|----3----|----4----|----5----|----6----|----7----|----8

   This file analysis the impact of upwind and downwind fires on MP 2.5 at the 
comuna by 6 hour time block level.  This requires files crossing fire with wind
direction generated by makeExposure.do, and MP 2.5 data generated by Ignacio at
the same comuna by 6 hour level.

   To run this file, all that is required is to change the location of the ROOT
directory on your computer in line 23.

*/

vers 14
clear all
set more off
cap log close


*-------------------------------------------------------------------------------
*--- (0) Globals and locals
*-------------------------------------------------------------------------------
global ROOT "~/investigacion/2022/climateChangeLatAm/replication"

global DAT "${ROOT}/data"
global OUT "${ROOT}/results"
global LOG "${ROOT}/log"

log using "$LOG/analysisMP25.txt", text replace
cap mkdir "$OUT/figures"

*-------------------------------------------------------------------------------
*--- (1) Descriptive of particulate matter over time (commented out)
*-------------------------------------------------------------------------------
local rundesc 0 //set this as 1 if wanting to make descriptive graph and table

if `rundesc'==1 {
    cd "$DAT/PM25"
    local mfiles
    local files
    foreach yr of numlist 2003(1)2021 {
        unzipfile Municipio_pm2p5_`yr'
        use "$DAT/PM25/Municipio_pm2p5_`yr'", clear
        split Date
        preserve
        // Collapse to daily exposure by country
        collapse pm2p5, by(Date1)
        gen date = date(Date1, "YMD")
        keep date pm2p5
        tempfile yr`yr'
        save `yr`yr''
        local files `files' `yr`yr''
        restore

        dis "hello"
        // Collapse to municipality by week exposure (sum stats ony)
        collapse pm2p5, by(Date1 CUT_2018)
        gen date = date(Date1, "YMD")
        gen accumDays    = date-14975
        gen weeksEgresos = ceil(accumDays/7)
        collapse pm2p5, by(weeksEgresos CUT_2018)
        gen year = `yr'
        tempfile m`yr'
        save `m`yr''
        local mfiles `mfiles' `m`yr''        
        rm Municipio_pm2p5_`yr'.dta
    }
    clear
    append using `files'
    format date %td

    // 20844 is peak of 2017 wildfires
    // 19729 is v large Melipilla wildfire (largest of 2013-2014)
    // Red lines are put 30 days to the right of these events for visualisation
    #delimit ;
    twoway line pm2p5 date, lcolor(blue%40)
    ytitle("Mean PM 2.5 (kg/m{sup:3})") xtitle("Date")
    xline(19759 20874, lcolor(red%65))
    xlabel(, format(%tdMon_dd,_CCYY))
    text(0.0000002   20874 "2017 Chile Wildfires", placement(e) size(vsmall))
    text(0.000000175 19759 "Melipilla 2014 (14,805 Ha)", placement(e) size(vsmall));
    #delimit cr
    graph export "$OUT/descriptives/pm25time.pdf", replace

    // Make summary stats corresponding to same period as hospitalization data
    clear
    append using `mfiles'
    keep if year>2003 & year<=2018
    collapse pm2p5, by(weeksEgresos CUT_2018 year)
    lab var pm2p5 "$ \text{PM}_{2.5} $ Concentration"
    lab var year "Year"
    estpost tabstat pm2p5, c(stat) stat(n mean sd min max)
    #delimit ;
    esttab using "$OUT/descriptives/pm25SumStats.tex", replace
    cells("count(fmt(%9.0gc)) mean sd min max")
    nonumber nomtitle nonote noobs label fragment collabels(none) nolines;
    #delimit cr
}

*-------------------------------------------------------------------------------
*--- (2) Match fires with MP 2.5
*-------------------------------------------------------------------------------
cd "$DAT/PM25"
local files
foreach year of numlist 2004(1)2021 {
    unzipfile Municipio_pm2p5_`year'
    dis "Year is `year'"
    use "$DAT/PM25/Municipio_pm2p5_`year'.dta", clear
    decode CUT_2018, gen(Cod_Comuna_2018)
    foreach dist of numlist 0(25)200 250 500 {
        #delimit ;
        merge 1:1 Date Cod_Comuna_2018 using
             "$DAT/windAndFires/fireWind_`year'_`dist'_donut5.dta";
        #delimit cr
        foreach type in upwind downwind nondownwind {
            foreach var of varlist `type' `type'Distance {
                rename `var' `var'_`dist' 
            }
        }
        local Fv F_exposure_0_45 F_exposure_45_90 F_exposure_90_135 F_exposure_135_180 
        foreach var of varlist `Fv' {
            rename `var' `var'_`dist'
        }
        sum upwind_`dist' downwind_`dist' nondownwind_`dist' fire
        #delimit ;
        keep pm2p5 Date Cod_Comuna_2018 CUT_2018 upwind* downwind*
                   nondownwind* windSpeed F_exposure*;
        #delimit cr
    }
    tempfile firemp`year'
    save `firemp`year''
    local files `files' `firemp`year''
    rm Municipio_pm2p5_`year'.dta
}
clear
append using `files'

sum windSpeed

foreach dist of numlist 0(25)200 250 {
    replace upwind_`dist'   = 0 if upwind_`dist'==.
    replace downwind_`dist' = 0 if downwind_`dist'==.
    replace nondownwind_`dist' = 0 if nondownwind_`dist'==.
}
foreach var of varlist F_exposure* {
    replace `var' = 0 if `var'==.
}

destring Cod_Comuna_2018, replace
gen region = floor(Cod_Comuna_2018/1000)
bys CUT_2018 (Date): gen time = _n

 
gen LB_up     = .
gen UB_up     = .
gen Beta_up   = .
gen LB_down   = .
gen UB_down   = .
gen Beta_down = .
gen distance  = .
gen equality  = .

*-------------------------------------------------------------------------------
*--- (3) Estimate models by the size of fire exposure and plot output
*-------------------------------------------------------------------------------
local j=1
sum windSpeed, d
local median = r(p50)
local opts absorb(i.region#i.time CUT_2018) cluster(CUT_2018)
foreach dist of numlist 0(25)200 250 {
    local X upwind_`dist' downwind_`dist'
    sum `X'
    reghdfe pm2p5 `X', `opts'
    replace Beta_up = _b[upwind_`dist'] in `j'
    replace LB_up   = _b[upwind_`dist']+invnormal(0.025)*_se[upwind_`dist'] in `j'
    replace UB_up   = _b[upwind_`dist']+invnormal(0.975)*_se[upwind_`dist'] in `j'

    replace Beta_down = _b[downwind_`dist'] in `j'
    replace LB_down   = _b[downwind_`dist']+invnormal(0.025)*_se[downwind_`dist'] in `j'
    replace UB_down   = _b[downwind_`dist']+invnormal(0.975)*_se[downwind_`dist'] in `j'
    test upwind_`dist'= downwind_`dist'
    replace equality = r(p) in `j'
    replace distance = `dist' in `j'
    local ++j
}
#delimit ;
twoway rarea LB_up UB_up distance, color(gs10%30)
   || line Beta_up distance, lcolor(red) lwidth(medthick) lpattern(solid)
   || rarea LB_down UB_down distance, color(gs10%30)
   || line Beta_down distance, lcolor(blue) lwidth(medthick) lpattern(dash)
ytitle("MP 2.5 (kg/m{sup:3})") xtitle("Fire Size (Ha)")
legend(order(2 "Estimate (upwind)" 4 "Estimate (downwind)" 1 "95% CI")
       position(6) rows(1)) yline(0, lpattern(dash));
#delimit cr
graph export "$OUT/figures/exposurePM25.pdf", replace
drop LB_* UB_* Beta_* distance equality

