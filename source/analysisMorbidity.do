/* analysisMorbity.do           damiancclarke              yyyy-mm-dd:2022-12-01
----|----1----|----2----|----3----|----4----|----5----|----6----|----7----|----8

  This file takes hospitalisations by week generated by Pedro and crosses them
with information on fires and wind direction to estimate the impact of exposure
to wild fire on hospitalisations. This requires data on fires crossed with wind
bearings which is generated by makeExposure.do and so prior to running this file
makeExposure.do should be run.

  To run this file, simply change the location of the global ROOT (line 24) to
the location of these materials on your computer.

THIS FILE IS CURRENTLY BEING CLEANED AND DOCUMENTED!!!!  

*/

vers 12
clear all
set more off
cap log close


*--------------------------------------------------------------------------------
*--- (0) globals locals
*--------------------------------------------------------------------------------
global ROOT "/home/damian/investigacion/2022/climateChangeLatAm/replication"


global DAT "${ROOT}/data"
global OUT "${ROOT}/results"
global LOG "${ROOT}/log"

cap mkdir "$OUT/figures"
cap mkdir "$OUT/tables"
cap mkdir "$OUT/descriptives"

log using "$LOG/analysis.txt", text replace

*--------------------------------------------------------------------------------
*--- (1) Make matches
*--------------------------------------------------------------------------------
cd "$DAT/health"
unzipfile "egresos_sem_final"
use egresos_sem_final

rename comuna_id Cod_Comuna_2018
rename semana weeksEgresos

rename Cod_Comuna_2018 numeric_Cod_Comuna_2018
gen Cod_Comuna_2018 = string(numeric_Cod_Comuna_2018, "%05.0f")
foreach H of numlist 0 50 100 150 200 250 500 {
    cap drop _merge
    #delimit ;
    merge 1:1 Cod_Comuna_2018 weeksEgresos
          using "$DAT/exposure/exposureWeekly_egresos_`H'ha_donut0";
    #delimit cr

    ren (upwind      upwindDistance)      (upwind`H'     upwindDistance`H')
    ren (downwind    downwindDistance)    (downwind`H' downwindDistance`H')
    ren (nondownwind nondownwindDistance) (nondownwind`H' nondownwindDistance`H')
    ren F_exposure_0_45    F_exposure_0_45_`H'
    ren F_exposure_45_90   F_exposure_45_90_`H'
    ren F_exposure_90_135  F_exposure_90_135_`H'
    ren F_exposure_135_180 F_exposure_135_180_`H'
}
#delimit ;
rename (Cod_Comuna_2018 numeric_Cod_Comuna_2018)
       (string_Cod_Comuna_2018 Cod_Comuna_2018);
#delimit cr

**DROP EGRESOS 2001-2002 (no fires yet)
keep if _merge==3

keep if year>2003&year<=2018
xtset Cod_Comuna_2018 weeksEgresos

*--------------------------------------------------------------------------------
*--- (2) Generate variables
*--------------------------------------------------------------------------------
**TOTALS
egen all_resp  = rowtotal(male_egr_resp* female_egr_resp*)
egen all_circ  = rowtotal(male_egr_circ* female_egr_circ*)
egen all_burn  = rowtotal(male_egr_quem* female_egr_quem*)
egen all_morb  = rowtotal(male_egr_0-male_egr_80 female_egr_0-female_egr_80)
egen all_pop   = rowtotal(INEfemale_0-INEmale_80)

foreach cause in morb resp circ burn {
    gen rate_`cause' = all_`cause'/all_pop*100000
}


**AGE-SPECIFIC
local age00_01 0
local age01_05 1_5
local age06_15 6_10 11_15
local age16_40 16_20 21_25 26_30 31_35 36_40
local age41_65 41_45 46_50 51_55 56_60 61_65
local age65_pl 66_70 71_75 76_80 80

foreach age in 00_01 01_05 06_15 16_40 41_65 65_pl {
    local pvars
    local avars
    local rvars
    local cvars
    local bvars

    local a1 = substr("`age'", 1, 2)
    local a2 = substr("`age'", 4, 2)
    local a2 "-`a2'"
    if `"`a2'"'=="-pl" local a2 " plus"
    if `"`a2'"'=="-01" local a2 "-1"
    if `"`a1'"'=="00"  local a1 "0"
    
    foreach level of local age`age' {
        local pvars `pvars' INEfemale_`level'  INEmale_`level' 
        local avars `avars' female_egr_`level' male_egr_`level'
        local rvars `rvars' female_egr_resp_`level' male_egr_resp_`level'
        local cvars `cvars' female_egr_circ_`level' male_egr_circ_`level'
        local bvars `bvars' female_egr_quem_`level' male_egr_quem_`level'
    }
    egen pop`age' = rowtotal(`pvars')
    egen morb_all_`age' = rowtotal(`avars')
    egen morb_resp_`age' = rowtotal(`rvars')
    egen morb_circ_`age' = rowtotal(`cvars')
    egen morb_burn_`age' = rowtotal(`bvars')

    foreach cause in all resp circ burn {
        if `"`cause'"'=="all"  local vlab "All cause hospitalizations"
        if `"`cause'"'=="resp" local vlab "Respiratory hospitalizations"
        if `"`cause'"'=="circ" local vlab "Circulatory hospitalizations"
        if `"`cause'"'=="burn" local vlab "Burns-related hospitalizations"

        gen rate_`cause'_`age' = (morb_`cause'_`age'/pop`age')*100000
        lab var rate_`cause'_`age' "`vlab' (Ages `a1'`a2')"
    }
}
sum pop*
sum rate*

*-------------------------------------------------------------------------------
*--- (3) Summary statistics and descriptives
*-------------------------------------------------------------------------------
lab var rate_morb "All cause hosptilzations (rate per 100,000)"
lab var rate_resp "Respiratory hospitaliztaions (rate per 100,000)"
lab var rate_circ "Circulatory hospitaliztaions (rate per 100,000)"
lab var rate_burn "Burns-related hospitaliztaions (rate per 100,000)"
lab var all_pop   "Population" 
lab var year      "Year"

    
local s1 rate_morb rate_resp rate_circ rate_burn
local s2 rate_all_00_01 rate_resp_00_01 rate_circ_00_01 rate_burn_00_01 
local s3 rate_all_65_pl rate_resp_65_pl rate_circ_65_pl rate_burn_65_pl 
local s4 all_pop year

estpost tabstat `s1' `s2' `s3' `s4', c(stat) stat(n mean sd min max)
#delimit ;
esttab using "$OUT/descriptives/hospitalSumStats.tex", replace 
cells("count(fmt(%9.0gc)) mean(fmt(%6.2fc)) sd(fmt(%6.2fc)) min max")
nonumber nomtitle nonote noobs label fragment collabels(none) nolines;
#delimit cr

local s1
local s2
foreach num of numlist 0 50 100 150 200 250 500 {
    lab var downwind`num' "Downwind fires $ \geq $ `num' Ha"
    lab var upwind`num'   "Upwind fires $ \geq $ `num' Ha"
    local s1 `s1' downwind`num'
    local s2 `s2' upwind`num'
}

estpost tabstat `s1' `s2', c(stat) stat(n mean sd min max)
#delimit ;
esttab using "$OUT/descriptives/firesSumStats_hosp.tex", replace 
cells("count(fmt(%9.0gc)) mean(fmt(%6.2fc)) sd(fmt(%6.2fc)) min max")
nonumber nomtitle nonote noobs label fragment collabels(none) nolines;
#delimit cr


*-------------------------------------------------------------------------------
*--- (3) Age by causes
*-------------------------------------------------------------------------------
gen LB_95 = . 
gen LB_90 = .
gen UB_95 = .
gen UB_90 = .
gen beta  = .
gen age = _n in 1/8

foreach size of numlist 500 200 0 50 100 150 250 {
    rename (upwind`size' downwind`size' nondownwind`size') (upwind downwind nondownwind)
    local xvars upwind downwind  humedad velocidad_viento temperatura
    local xvars upwind downwind nondownwind
    
    foreach cause in all resp circ burn {
        if `"`cause'"'=="all" local extra 1day 1month
        local extra
        local j = 1
        foreach age in `extra' 00_01 01_05 06_15 16_40 41_65 65_pl {
            dis "Cause `cause', age `age' (size `size')"
            #delimit ;
            reghdfe rate_`cause'_`age' `xvars' [aw=pop`age'],
                abs(Cod_Comuna_2018 i.region_id#i.weeksEgresos)
                vce(cluster Cod_Comuna_2018 weeksEgresos);
            #delimit cr
            replace beta  = _b[upwind] in `j'
            replace UB_95 = _b[upwind]+invnormal(0.975)*_se[upwind] in `j'
            replace LB_95 = _b[upwind]+invnormal(0.025)*_se[upwind] in `j'
            replace UB_90 = _b[upwind]+invnormal(0.950)*_se[upwind] in `j'
            replace LB_90 = _b[upwind]+invnormal(0.050)*_se[upwind] in `j'            
            local ++j
        }
        if `"`cause'"'=="XXall" {
            #delimit ;
            twoway rcap LB_95 UB_95 age in 1/8, lcolor(black%50)
            ||     rcap LB_90 UB_90 age in 1/8, lwidth(thick)  lcolor(black%50)
            ||     scatter beta age     in 1/8, msize(medlarge) ms(S) mcolor(black)
            xlabel(1 "24 hour"  2 "< 1 month" 3 "0-1 year" 4 "1-5 years" 5 "6-15 years"
                   6 "16-40 years" 7 "41-65 years" 8 "{&ge} 65 years", angle(45))
            yline(0) legend(order(3 "Point estimate" 1 "95% CI" 2 "90% CI")
                            position(6) rows(1)) ytitle("Effect of Upwind Exposure")
            xtitle("Age group") ylabel(, format(%04.2f));
            graph export "$OUT/figures/morb_`cause'_`size'.pdf", replace; 
            #delimit cr
        }
        else {
            #delimit ;
            twoway rcap LB_95 UB_95 age in 1/6, lcolor(black%50)
            ||     rcap LB_90 UB_90 age in 1/6, lwidth(thick)  lcolor(black%50)
            ||     scatter beta age     in 1/6, msize(medlarge) ms(S) mcolor(black)
            xlabel(1 "0-1" 2 "1-5" 3 "6-15" 4 "16-40" 5 "41-65" 6 "{&ge} 65", angle(45))
            yline(0) legend(order(3 "Point estimate" 1 "95% CI" 2 "90% CI")
                            position(6) rows(1)) ytitle("Effect of Upwind Exposure")
            xtitle("Age group") ylabel(, format(%04.2f));
            graph export "$OUT/figures/morb_`cause'_`size'.pdf", replace; 
            #delimit cr
        }
    }
    rename (upwind downwind nondownwind) (upwind`size' downwind`size' nondownwind`size')
}


*-------------------------------------------------------------------------------
*--- (4) Fire sizes effects
*-------------------------------------------------------------------------------
local opts abs(Cod_Comuna_2018 i.region_id#i.weeksEgresos)  cluster(Cod_Com)
foreach size of numlist 50 100 150 250 500 {
    rename (upwind`size' downwind`size' nondownwind`size') (upwind downwind nondownwind)
    local xvars upwind downwind 
    eststo: reghdfe rate_resp `xvars' [aw=all_pop], `opts'
    sum rate_resp if e(sample)==1
    estadd scalar yvar = r(mean)
    sum upwind if e(sample)==1
    estadd scalar x_up   = r(mean)
    sum downwind if e(sample)==1
    estadd scalar x_down = r(mean)
    
    eststo: reghdfe rate_burn `xvars' [aw=all_pop], `opts'
    sum rate_burn if e(sample)==1
    estadd scalar yvar = r(mean)
    sum upwind if e(sample)==1
    estadd scalar x_up   = r(mean)
    sum downwind if e(sample)==1
    estadd scalar x_down = r(mean)
    rename (upwind downwind nondownwind) (upwind`size' downwind`size' nondownwind`size')
}
gen upwind      = .
gen downwind    = .
gen nondownwind = .
lab var upwind      "Upwind fires during week"
lab var downwind    "Downwind fires during week"
lab var nondownwind "Non-Downwind fires during week"

#delimit ;
esttab est1 est3 est5 est7 est9 est2 est4 est6 est8 est10 using "$OUT/tables/RFhosp_respBurn.tex", 
replace booktabs cells(b(fmt(%-9.3f) star) se(fmt(%-9.3f) par(( )) )) label
stats(yvar x_up x_down N r2, fmt(%5.3f %5.3f %5.3f %9.0fc %04.2f)
      label("\\ Mean of Dep. Var." "Mean Upwind" "Mean Non-Upwind" Observations R-Squared))
nonotes nogaps mlabels(, none) nonumbers style(tex) fragment noline keep(`xvars')
collabels(none) starlevel("*" 0.1 "**" 0.05 "***" 0.01);
#delimit cr
estimates clear
drop upwind downwind nondownwind


foreach size of numlist 50 100 150 250 {
    rename (upwind`size' downwind`size' nondownwind`size') (upwind downwind nondownwind)
    local xvars upwind downwind 
    eststo: reghdfe rate_circ `xvars' [aw=all_pop], abs(Cod_Comuna_2018 i.region_id#i.weeksEgresos)  cluster(Cod_Com)
    sum rate_circ if e(sample)==1
    estadd scalar yvar = r(mean)
    sum upwind if e(sample)==1
    estadd scalar x_up   = r(mean)
    sum downwind if e(sample)==1
    estadd scalar x_down = r(mean)
    
    eststo: reghdfe rate_burn `xvars' [aw=all_pop], abs(Cod_Comuna_2018 i.region_id#i.weeksEgresos)  cluster(Cod_Com)
    sum rate_burn if e(sample)==1
    estadd scalar yvar = r(mean)
    sum upwind if e(sample)==1
    estadd scalar x_up   = r(mean)
    sum downwind if e(sample)==1
    estadd scalar x_down = r(mean)
    rename (upwind downwind nondownwind) (upwind`size' downwind`size' nondownwind`size')
}
gen upwind      = .
gen downwind    = .
gen nondownwind = .
lab var upwind      "Upwind fires during week"
lab var downwind    "Downwind fires during week"
lab var nondownwind "Non-Downwind fires during week"

#delimit ;
esttab est1 est3 est5 est7 est2 est4 est6 est8 using "$OUT/tables/RFhosp_circburntex", 
replace booktabs cells(b(fmt(%-9.3f) star) se(fmt(%-9.3f) par(( )) )) label
stats(yvar x_up x_down N r2, fmt(%5.3f %5.3f %5.3f %9.0fc %04.2f)
      label("\\ Mean of Dep. Var." "Mean Upwind" "Mean Non-Upwind" Observations R-Squared))
nonotes nogaps mlabels(, none) nonumbers style(tex) fragment noline keep(`xvars')
collabels(none) starlevel("*" 0.1 "**" 0.05 "***" 0.01);
#delimit cr
estimates clear
drop upwind downwind nondownwind

*-------------------------------------------------------------------------------
*--- (5) Ages
*-------------------------------------------------------------------------------
foreach size of numlist 50 200 {
    rename (upwind`size' downwind`size' nondownwind`size') (upwind downwind nondownwind)
    local xvars upwind downwind nondownwind
    foreach var of varlist rate_all_00_01 rate_all_01_05 rate_all_06_15 rate_all_16_40 rate_all_41_65 rate_all_65_pl {
        eststo: reghdfe `var' `xvars' [aw=all_pop], abs(Cod_Comuna_2018 i.region_id#i.weeksEgresos)  cluster(Cod_Com)
        sum `var' if e(sample)==1
        estadd scalar yvar = r(mean)
    }    
    rename (upwind downwind nondownwind) (upwind`size' downwind`size' nondownwind`size')
}

gen upwind      = .
gen downwind    = .
gen nondownwind = .
lab var upwind      "Upwind fires during week"
lab var downwind    "Downwind fires during week"
lab var nondownwind "Non-Downwind fires during week"
#delimit ;
esttab est1 est7 est2 est8 est6 est12 using "$OUT/tables/RFhosp_ages_sensitive.tex", 
replace booktabs cells(b(fmt(%-9.3f) star) se(fmt(%-9.3f) par(( )) )) label
stats(yvar N r2, fmt(%5.3f %9.0fc %04.2f)
      label("\\ Mean of Dep. Var." Observations R-Squared))
nonotes nogaps mlabels(, none) nonumbers style(tex) fragment noline
keep(upwind downwind) collabels(none) starlevel("*" 0.1 "**" 0.05 "***" 0.01);

esttab est3 est9 est4 est10 est5 est11 using "$OUT/tables/RFhosp_ages_nonsensitive.tex", 
replace booktabs cells(b(fmt(%-9.3f) star) se(fmt(%-9.3f) par(( )) )) label
stats(yvar N r2, fmt(%5.3f %9.0fc %04.2f)
      label("\\ Mean of Dep. Var." Observations R-Squared))
nonotes nogaps mlabels(, none) nonumbers style(tex) fragment noline
keep(upwind downwind) collabels(none) starlevel("*" 0.1 "**" 0.05 "***" 0.01);
#delimit cr
estimates clear
drop upwind downwind nondownwind



foreach num of numlist 0 50(50)250 500 {
    gen beta`num' = .
    gen LB`num'   = .
    gen UB`num'   = .

    gen betaD`num' = .
    gen LBD`num'   = .
    gen UBD`num'   = .
}
gen ages = .

local A = 1
foreach size of numlist 0 50 100 150 200 250 500 {
    rename (upwind`size' nondownwind`size' downwind`size') (upwind downwind nondownwind)
    reghdfe rate_all_00_01 upwind downwind nondownwind [aw=all_pop], abs(Cod_Comuna_2018 i.region_id#i.weeksEgresos)  cluster(Cod_Com)    
    replace beta`size' = _b[upwind] in `A'
    replace LB`size'   = _b[upwind]+invnormal(0.05)*_se[upwind] in `A'
    replace UB`size'   = _b[upwind]+invnormal(0.95)*_se[upwind] in `A'        
    replace betaD`size' = _b[downwind] in `A'
    replace LBD`size'   = _b[downwind]+invnormal(0.05)*_se[downwind] in `A'
    replace UBD`size'   = _b[downwind]+invnormal(0.95)*_se[downwind] in `A'        
    rename (upwind downwind nondownwind) (upwind`size' downwind`size' nondownwind`size')
}
replace ages=`A' in `A'

foreach a1 of numlist 1(5)76 {
    local ++A
    local a2 = `a1'+4
    gen age_`a1'_`a2' = (male_egr_`a1'_`a2'+female_egr_`a1'_`a2')/(INEmale_`a1'_`a2'+INEfemale_`a1'_`a2')*100000
    foreach size of numlist 0 50 100 150 200 250 500 {
        rename (upwind`size' nondownwind`size' downwind`size') (upwind downwind nondownwind)
        reghdfe age_`a1'_`a2' upwind downwind nondownwind [aw=all_pop], abs(Cod_Comuna_2018 i.region_id#i.weeksEgresos)  cluster(Cod_Com)    
        replace beta`size' = _b[upwind] in `A'
        replace LB`size'   = _b[upwind]+invnormal(0.05)*_se[upwind] in `A'
        replace UB`size'   = _b[upwind]+invnormal(0.95)*_se[upwind] in `A'        
        replace betaD`size' = _b[downwind] in `A'
        replace LBD`size'   = _b[downwind]+invnormal(0.05)*_se[downwind] in `A'
        replace UBD`size'   = _b[downwind]+invnormal(0.95)*_se[downwind] in `A'        
        rename (upwind downwind nondownwind) (upwind`size' downwind`size' nondownwind`size')
    }
    replace ages = `A' in `A'
}
local ++A
gen age_80_pl = (male_egr_80+female_egr_80)/(INEmale_80+INEfemale_80)*100000
foreach size of numlist 0 50 100 150 200 250 500 {
    rename (upwind`size' downwind`size' nondownwind`size') (upwind downwind nondownwind)
    reghdfe age_80_pl upwind downwind nondownwind [aw=all_pop], abs(Cod_Comuna_2018 i.region_id#i.weeksEgresos)  cluster(Cod_Com)    
    replace beta`size' = _b[upwind] in `A'
    replace LB`size'   = _b[upwind]+invnormal(0.05)*_se[upwind] in `A'
    replace UB`size'   = _b[upwind]+invnormal(0.95)*_se[upwind] in `A'        
    replace betaD`size' = _b[downwind] in `A'
    replace LBD`size'   = _b[downwind]+invnormal(0.05)*_se[downwind] in `A'
    replace UBD`size'   = _b[downwind]+invnormal(0.95)*_se[downwind] in `A'        
    rename (upwind downwind nondownwind) (upwind`size' downwind`size' nondownwind`size')
}
replace ages = `A' in `A'

foreach size of numlist 0 50 100 150 200 250 500 {
    colorpalette viridis, n(4) nograph
    #delimit ;
    twoway rcap LB`size' UB`size' ages, lcolor("`r(p1)'") || scatter beta`size' ages, mcolor("`r(p3)'") ms(Sh)
    xlabel(1  "0-1"    2 "1-5"   3 "6-10"   4 "11-15"  5 "16-20"  6 "21-25"   7 "26-30"  8 "31-35" 9 "36-40"
           10 "41-45" 11 "46-50" 12 "51-55" 13 "56-60" 14 "61-65" 15 "66-70'" 16 "71-75" 17 "76-80" 18 ">80", angle(45))
    legend(order(2 "Point Estimate" 1 "90% CI") rows(1) position(6)) ytitle("Impact of Upwind Exposure")
    ylabel(-5(1)5, format(%03.1f))
    xtitle(" " " " "Age Group") yline(0, lcolor(red) lpattern(dash));
    graph export "$OUT/figures/ageEffects_upwind_`size'.eps", replace;

    colorpalette viridis, n(4) nograph;
    twoway rcap LBD`size' UBD`size' ages, lcolor("`r(p1)'") || scatter betaD`size' ages, mcolor("`r(p3)'") ms(Sh)
    xlabel(1  "0-1"    2 "1-5"   3 "6-10"   4 "11-15"  5 "16-20"  6 "21-25"   7 "26-30"  8 "31-35" 9 "36-40"
           10 "41-45" 11 "46-50" 12 "51-55" 13 "56-60" 14 "61-65" 15 "66-70'" 16 "71-75" 17 "76-80" 18 ">80", angle(45))
    legend(order(2 "Point Estimate" 1 "90% CI") rows(1) position(6)) ytitle("Impact of Non-Upwind Exposure")
    ylabel(-5(1)5, format(%03.1f))
    xtitle(" " " " "Age Group") yline(0, lcolor(red) lpattern(dash));
    #delimit cr
    graph export "$OUT/figures/ageEffects_downwind_`size'.eps", replace
}

